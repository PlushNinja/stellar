$.fn.extend
  initialize: ->

    $('#static-wrap a.snip').each ->
      $.ajax
        url: $(this).attr 'href'
        success: (data, status, xhr) =>
          $(this).replaceWith data

    $('.gallery', this).each ->
      $(this).magnificPopup
        delegate: '.image-link'
        type: 'image'
        gallery:
          enabled: true

    $('[data-toggle="tooltip"]', this).tooltip()
    $('[data-toggle="popover"]', this).popover
      html: true
      trigger: 'hover'

    $('.selectize', this).selectize
      plugins: ['remove_button']
      dropdownParent: 'body'
      allowEmptyOption: true

    $('.infinite-table', this).infinitePages
      loading: ->
        $(this).html('<i class="fa fa-spin fa-circle-o-notch"></i>')

    $('.credit-card-number').payment 'formatCardNumber'
    $('.credit-card-expiry').payment 'formatCardExpiry'
    $('.credit-card-cvc').payment 'formatCardCVC'

  deactivate: ->
    $(this).removeClass 'active'
  activate: ->
    $(this).addClass 'active'
    $(this).parents('li').addClass 'active'

  make_room: (element) ->
    $(this).css 'padding-bottom', element.height()

  rebuild_masonry: ->
    $brickwork = $('.brickwork')
    $brickwork.masonry('destroy') if $brickwork.hasClass('masonry')
    $brickwork.imagesLoaded ->
      $brickwork.masonry
        itemSelector: 'li.product'
        percentPosition: false

jQuery ->

  window.wiselinks = new Wiselinks()

  $('body').css('margin-bottom', 30 + $('.footer').height())

  $(document).initialize()
  $(document).rebuild_masonry()

  new Clipboard('.clipboard')

  $(document).on 'change', 'form.immediate', (e) ->
    $(e.currentTarget).trigger('submit.rails')

  $(document).on 'click', '[data-toggle=sidebar]', (e) ->
    $('.sidebar').toggleClass 'active'

  $(document).on 'click', 'a[href="#back"]', (e) ->
    e.preventDefault()
    history.back()

  $(document).on 'click', 'a[href="#print"]', (e) ->
    e.preventDefault()
    $.print()

  $(document).on 'page:loading', -> NProgress.start()
  $(document).on 'page:always', -> NProgress.done()

  $(document).on 'page:done', (event, $target, status, url, data) ->
    $.SmartMenus.Bootstrap.init()
    $(document).rebuild_masonry()
    if ga? then ga('send', 'pageview', window.location.pathname)
