$.fn.extend
  initialize: ->

    $('#static-wrap a[href^="/snippets/"]').each ->
      $.ajax
        url: $(this).attr 'href'
        success: (data, status, xhr) =>
          $(this).replaceWith data

    $('.gallery', this).each ->
      $(this).magnificPopup
        delegate: '.image-link'
        type: 'image'
        gallery:
          enabled: true

    $('[data-toggle="tooltip"]', this).tooltip()
    $('[data-toggle="popover"]', this).popover
      html: true
      trigger: 'hover'

    $('.selectize', this).selectize
      plugins: ['remove_button']
      dropdownParent: 'body'
      allowEmptyOption: true

    $('.infinite-table', this).infinitePages
      loading: ->
        $('#spinner').show()

    $('.sticky', this).each ->
      $(this).stick_in_parent $(this).data

    $('.credit-card-number').payment 'formatCardNumber'
    $('.credit-card-expiry').payment 'formatCardExpiry'
    $('.credit-card-cvc').payment 'formatCardCVC'

  deactivate: ->
    $(this).removeClass 'active'
  activate: ->
    $(this).addClass 'active'
    $(this).parents('li').addClass 'active'

  make_room: (element) ->
    $(this).css 'padding-bottom', element.height()

  rebuild_parallax: ->
    $('.parallax-mirror').remove()
    $parallax = $('.parallax-window')
    $parallax.imagesLoaded ->
      $parallax.parallax($parallax.data())

  rebuild_masonry: ->
    $masonry = $('.masonry-grid')
    $masonry.imagesLoaded ->
      $masonry.masonry
        itemSelector: '.masonry-item'
        columnWidth: '.single'
        percentPosition: true
      Wiselinks.scrollPage()

jQuery ->

  $(document).initialize()
  $(document).rebuild_masonry()

  new Clipboard('.clipboard')

  $(document).on 'change', 'form.immediate', (e) ->
    $(e.currentTarget).trigger('submit.rails')

  $(document).on 'click', '[data-toggle=sidebar]', (e) ->
    $('.sidebar').toggleClass 'active'

  $(document).on 'click', 'a[href="#back"]', (e) ->
    e.preventDefault()
    history.back()

  $(document).on 'click', 'a[href="#print"]', (e) ->
    e.preventDefault()
    $.print()
